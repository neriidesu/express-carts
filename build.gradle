plugins {
    id 'fabric-loom' version '1.10-SNAPSHOT'
    id 'com.modrinth.minotaur' version '2.+'
    id 'com.github.breadmoirai.github-release' version '2.4.1'
}

version = "${project.mod_version}+${project.minecraft_version}"
group = project.maven_group

base {
    archivesName = project.archives_base_name
}

repositories {
    maven { url 'https://maven.nucleoid.xyz' }

    maven {
        name = 'ParchmentMC'
        url = 'https://maven.parchmentmc.org'
    }

    // https://github.com/xpple/BetterConfig/
    maven {
        url 'https://maven.xpple.dev/maven2'
    }
}

loom {
    splitEnvironmentSourceSets()

    mods {
        "expresscarts" {
            sourceSet sourceSets.main
            sourceSet sourceSets.client
        }
    }

}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    mappings loom.layered() {
        officialMojangMappings() {
            nameSyntheticMembers = true
        }
        // TODO: fix when Parchment is available for 1.21.6
        // parchment("org.parchmentmc.data:parchment-${project.minecraft_version}:${project.parchment_version}@zip")
    }

    modImplementation include("eu.pb4:polymer-core:${project.polymer_version}")
    modImplementation include("eu.pb4:polymer-resource-pack:${project.polymer_version}")
    modImplementation include("xyz.nucleoid:server-translations-api:${project.server_translations_api_version}")

    modImplementation include("dev.xpple:betterconfig-fabric:${project.betterconfig_version}")

    include(implementation(annotationProcessor("io.github.llamalad7:mixinextras-fabric:${project.mixinextras_version}")))

}

processResources {
    inputs.property "version", project.version
    inputs.property "minecraft_version", project.minecraft_version
    inputs.property "loader_version", project.loader_version
    filteringCharset "UTF-8"

    filesMatching("fabric.mod.json") {
        expand "version": project.version,
                "minecraft_version": project.minecraft_version,
                "loader_version": project.loader_version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 21
}

java {
    // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
    // if it is present.
    // If you remove this line, sources will not be generated.
    withSourcesJar()

    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.base.archivesName.get()}" }
    }
}

def changelogFile = file("CHANGELOG.md")
def changelogText = changelogFile.exists() ? changelogFile.readLines("UTF-8").join("\n") : ""
def readmeFile = file("README.md")
def readmeText = readmeFile.exists() ? readmeFile.readLines("UTF-8").join("\n") : ""


modrinth {
    token = System.getenv("MODRINTH_TOKEN")
    projectId = "express-carts"
    versionName = "${project.mod_version} for Fabric ${project.minecraft_version}"
    uploadFile = remapJar
    changelog = changelogText
    syncBodyFrom = readmeText
    dependencies {
        required.project "fabric-api"
        embedded.project "polymer"
        embedded.project "betterconfig"
    }
}
tasks.modrinth.onlyIf {
    !changelogText.isBlank() && !readmeText.isBlank()
}
tasks.modrinth.dependsOn(tasks.modrinthSyncBody)

githubRelease {
    token((System.getenv("GITHUB_TOKEN") ?: ""))
    owner "seercat3160"
    repo "express-carts"
    tagName "${project.mod_version}+${project.minecraft_version}"
    targetCommitish "1.21.6"
    releaseName "${project.mod_version} for Fabric ${project.minecraft_version}"
    body changelogText
    draft true
    releaseAssets remapJar.outputs
}
tasks.githubRelease.onlyIf {
    !changelogText.isBlank()
}